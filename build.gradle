buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.49")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.owasp.dependencycheck' version '5.0.0'
    id 'org.sonarqube' version '2.6.2'
    id 'jacoco'
    id 'pmd'
    id 'info.solidsoft.pitest' version '1.4.0'
}

apply plugin: 'checkstyle'
apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.reform.divorce'
version = '3.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    maxWarnings = 0
    toolVersion = '8.21'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, './')
}

bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
    archiveName 'div-fees-and-payment-service.jar'
}

dependencyManagement {
    dependencies {
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
        dependencySet(
                group: 'com.fasterxml.jackson.core',
                version: '2.10.0'
        ) {
            entry 'jackson-core'
            entry 'jackson-databind'
        }
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs = ['src/integrationTest/java']
        }
        resources {
            srcDirs = ['src/integrationTest/resources']
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

mainClassName = 'uk.gov.hmcts.reform.divorce.feepayment.FeesPaymentServiceApplication'

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()

    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }

}

distributions {
    main {
        contents {
            from(file("$projectDir/lib/AI-Agent.xml")) {
                into "lib"
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.divorce.feepayment.*']
    excludedClasses = ['uk.gov.hmcts.reform.divorce.feepayment.health.*',
                       'uk.gov.hmcts.reform.divorce.feepayment.management.config.*',
                       'uk.gov.hmcts.reform.divorce.feepayment.config.*',
                       'uk.gov.hmcts.reform.divorce.feepayment.model.*']
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

def versions = [
        commonsBeanUtils:                               '1.9.4',
        commonsIo:                                      '2.5',
        commonsLang3:                                   '3.0',
        guava:                                          '27.0.1-jre',
        gradlePitest:                                   '1.4.0',
        hibernate:                                      '6.0.5.Final',
        jacksonDatabind:                                '2.10.0',
        jacksonAnnotations:                             '2.9.9',
        javaxValidation:                                '2.0.0.Final',
        jaywayJsonPath:                                 '2.2.0',
        pitest:                                         '1.3.2',
        puppyCrawl:                                     '8.21',
        reformHealth:                                   '0.0.3',
        reformsHttpProxySpringBootAutoconfigure:        '2.0.0',
        reformsJavaLogging:                             '5.0.1',
        serviceTokenGenerator:                          '2.0.0',
        sonarPitest:                                    '0.5',
        spring_security_rsa:                            '1.0.8.RELEASE',
        springBoot:                                     '2.1.2.RELEASE',
        springfoxSwagger:                               '2.9.2',
        springHateoas:                                  '0.25.1.RELEASE',
        springOpenFeign:                                '2.1.1.RELEASE',
        springPluginCore:                               '1.2.0.RELEASE',
        springRetry:                                    '1.2.4.RELEASE',
        wiremockVersion:                                '2.8.0',
        springSecurityCrypto:                           '5.1.5.RELEASE',
        tomcat:                                         '9.0.22',
        lombok:                                         '1.16.16'
]

dependencies {
    //integration test
    integrationTestCompile(sourceSets.test.output)
    integrationTestCompile group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
    integrationTestCompile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '5.1'
    integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.3'
    integrationTestCompile group: 'junit', name: 'junit', version: '4.12'
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-core', version: '1.5.2'
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: '1.5.2'
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: '1.5.2'
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: '1.5.2'
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version:'1.1.3'
    integrationTestCompile group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: '0.6.0'
    integrationTestCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    integrationTestCompile group: 'org.projectlombok', name: 'lombok', version: '1.16.16'
    integrationTestCompile group: 'org.skyscreamer', name:'jsonassert', version: '1.2.3'
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'

    compile (group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils) {
        force = true
    }

    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind) {
        force = true
    }
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jacksonAnnotations
    compile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: versions.jaywayJsonPath
    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    compile group: 'javax.validation', name: 'validation-api', version: versions.javaxValidation
    compile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    compile("org.springframework.boot:spring-boot-devtools")
    compile group: 'org.hibernate', name: 'hibernate-validator', version: versions.hibernate
    compile group: 'com.google.guava', name: 'guava', version: versions.guava
    compile (group:  'org.springframework.security', name: 'spring-security-rsa', version: versions.spring_security_rsa) {
        force = true
    }

    compile (group:  'org.springframework.security', name: 'spring-security-crypto', version: versions.springSecurityCrypto) {
        force = true
    }

    compile (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator){
        exclude group: 'io.reactivex', module: 'rxnetty'
        exclude group: 'io.reactivex', module: 'rxnetty-servo'
        exclude group: 'io.reactivex', module: 'io.reactivex'
        exclude group: 'io.reactivex', module: 'rxnetty-contexts'
    }

    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-core', version: versions.tomcat) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-websocket', version: versions.tomcat) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-el', version: versions.tomcat) {
        force = true
    }

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    implementation("org.projectlombok:lombok:$versions.lombok")
    testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '5.1'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springOpenFeign
    compile group: 'org.springframework.retry', name: 'spring-retry', version: versions.springRetry
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: versions.springHateoas
    compile group: 'org.springframework.plugin', name: 'spring-plugin-core', version: versions.springPluginCore
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

    compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: versions.reformHealth
    compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: versions.reformsHttpProxySpringBootAutoconfigure
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.reformsJavaLogging

    testCompile group: 'com.github.tomakehurst', name:'wiremock', version: versions.wiremockVersion
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
        exclude(module: 'commons-logging')
    }

    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version: versions.puppyCrawl

    compile group: 'org.pitest', name: 'pitest', version: versions.pitest
    compile group:'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    compile group:'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: versions.sonarPitest
}

test {
    exclude '**/HealthCheckFunctionalTest.class'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    include "uk/gov/hmcts/**/smoketest/**"
}


task functional(type: Test, description: 'Runs the functional tests', group: 'Verification') {
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    finalizedBy aggregate
}

pmd {
    toolVersion = "6.11.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    // remove gradle default rulesets
    ruleSets = []
    ruleSetFiles = files("ruleset.xml")
}

def sonarExclusions = [
        '**uk/gov/hmcts/reform/divorce/feepayment/model/*',
        '**uk/gov/hmcts/reform/divorce/feepayment/FeesPaymentServiceApplication.java'
]

sonarqube {
    properties {
        property "sonar.projectKey", "DivorceFeesAndPaymentsService"
        property "sonar.projectName", "Divorce :: Fees and Payments Service"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/functional.exec"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

jacocoTestReport {
    executionData(test, functional)

    reports {
        xml {
            enabled true
        }

        html {
            enabled true
        }
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

def debug = System.getProperty("debug")
run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5010']
    }
}
