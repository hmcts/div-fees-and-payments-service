buildscript {
    ext {
        springBootVersion = '2.3.11.RELEASE'
    }
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.5.7")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.6.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.owasp.dependencycheck' version '6.2.0'
    id 'org.sonarqube' version '3.2.0'
    id 'org.springframework.boot' version '2.3.11.RELEASE'
    id 'au.com.dius.pact' version '4.1.8'
}

apply plugin: 'checkstyle'
apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.reform.divorce'
version = '3.0.0'
sourceCompatibility = 11
targetCompatibility = 11

checkstyle {
    maxWarnings = 0
    toolVersion = '8.35'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, './')
    checkstyle.configFile = new File(rootDir, "checkstyle.xml")
}

bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version
    }
    archiveName 'div-fees-and-payment-service.jar'
}

dependencyManagement {
    dependencies {
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

        dependency group: 'org.yaml', name: 'snakeyaml', version: '1.28'

        dependency group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.68'
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs = ['src/integrationTest/java']
        }
        resources {
            srcDirs = ['src/integrationTest/resources']
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
            include "uk/gov/hmcts/reform/divorce/feeandpaymentservice/**"

        }
        resources.srcDir file('src/contractTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

mainClassName = 'uk.gov.hmcts.reform.divorce.feepayment.FeesPaymentServiceApplication'

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()

    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }
}

distributions {
    main {
        contents {
            from(file("$projectDir/lib/AI-Agent.xml")) {
                into "lib"
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.divorce.feepayment.*']
    excludedClasses = ['uk.gov.hmcts.reform.divorce.feepayment.health.*',
                       'uk.gov.hmcts.reform.divorce.feepayment.management.config.*',
                       'uk.gov.hmcts.reform.divorce.feepayment.config.*',
                       'uk.gov.hmcts.reform.divorce.feepayment.model.*']
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

def versions = [
        commonsBeanUtils:                               '1.9.4',
        commonsIo:                                      '2.7',
        commonsLang3:                                   '3.9',
        gradlePitest:                                   '1.4.6',
        guava:                                          '30.1-jre',
        hibernate:                                      '6.0.5.Final',
        javaxValidation:                                '2.0.0.Final',
        jaywayJsonPath:                                 '2.4.0',
        jsonassert:                                     '1.2.3',
        junit:                                          '4.13.1',
        lombok:                                         '1.18.20',
        nimbusJoseJwt:                                  '8.5',
        pitest:                                         '1.4.10',
        puppyCrawl:                                     '8.35',
        reformsJavaLogging:                             '5.1.7',
        restAssured:                                    '4.2.1',
        serviceTokenGenerator:                          '2.0.0',
        serenity:                                       '2.5.8',
        serenityCucumber:                               '1.9.51',
        serviceAuthProviderClient:                      '0.6.0',
        sonarPitest:                                    '0.5',
        springBoot:                                     '2.3.11.RELEASE',
        springfoxSwagger:                               '2.9.2',
        springHateoas:                                  '0.25.2.RELEASE',
        springOpenFeign:                                '2.2.1.RELEASE',
        springPluginCore:                               '1.2.0.RELEASE',
        springRetry:                                    '1.2.5.RELEASE',
        springSecurityCrypto:                           '5.4.5',
        springSecurityRsa:                              '1.0.9.RELEASE',
        tomcat:                                         '9.0.50',
        wiremockVersion:                                '2.27.2',
        unirest:                                        '1.4.9',
        pact_version:                                   '4.1.8',
        jetty:                                          '9.4.35.v20201120',
        httpComponents:                                 '4.5.13',
]

dependencies {
    //integration test
    integrationTestCompile(sourceSets.test.output)
    integrationTestCompile group: 'com.mashape.unirest', name: 'unirest-java', version: versions.unirest
    integrationTestCompile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: versions.nimbusJoseJwt
    integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    integrationTestCompile group: 'io.rest-assured', name: 'rest-assured-all', version: versions.restAssured
    integrationTestCompile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    integrationTestCompile group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestCompile group: 'org.skyscreamer', name:'jsonassert', version: versions.jsonassert
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    integrationTestCompile group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '3.0.1'
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    integrationTestCompile group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceAuthProviderClient

    compile (group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils) {
        force = true
    }
    compile (group: 'org.springframework.security', name: 'spring-security-rsa', version: versions.springSecurityRsa) {
        force = true
    }
    compile (group: 'org.springframework.security', name: 'spring-security-crypto', version: versions.springSecurityCrypto) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-core', version: versions.tomcat) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-websocket', version: versions.tomcat) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-el', version: versions.tomcat) {
        force = true
    }

    compile (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator){
        exclude group: 'io.reactivex', module: 'io.reactivex'
        exclude group: 'io.reactivex', module: 'rxnetty'
        exclude group: 'io.reactivex', module: 'rxnetty-contexts'
        exclude group: 'io.reactivex', module: 'rxnetty-servo'
    }

    compile("org.springframework.boot:spring-boot-devtools")
    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    implementation("org.projectlombok:lombok:$versions.lombok")
    testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: versions.nimbusJoseJwt
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    compile group: 'com.google.guava', name: 'guava', version: versions.guava
    compile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: versions.jaywayJsonPath
    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version: versions.puppyCrawl
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
    compile group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    compile group: 'javax.validation', name: 'validation-api', version: versions.javaxValidation
    compile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    compile group: 'junit', name: 'junit', version: versions.junit
    compile group: 'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: versions.sonarPitest
    compile group: 'org.hibernate', name: 'hibernate-validator', version: versions.hibernate
    compile group: 'org.pitest', name: 'pitest', version: versions.pitest

    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springOpenFeign
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: versions.springHateoas
    // DO NOT UPDATE spring-plugin-core - 2.0.0.RELEASE is available but not compatible with springfox 2.9 and above
    // see here https://github.com/springfox/springfox/issues/2932
    compile group: 'org.springframework.plugin', name: 'spring-plugin-core', version: versions.springPluginCore
    compile group: 'org.springframework.retry', name: 'spring-retry', version: versions.springRetry

    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-httpcomponents', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.reformsJavaLogging
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: versions.httpComponents

    testCompile group: 'org.eclipse.jetty', name: 'jetty-client', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-continuation', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-util-ajax', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-http', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-security', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-util', version: versions.jetty
    testCompile group: 'org.eclipse.jetty', name: 'jetty-xml', version: versions.jetty
    testCompile group: 'com.github.tomakehurst', name:'wiremock', version: versions.wiremockVersion
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
        exclude(module: 'commons-logging')
    }
    testCompile('com.github.hmcts:fortify-client:1.2.0:all')

    contractTestCompile("org.junit.jupiter:junit-jupiter-api:5.7.2")
    contractTestRuntime("org.junit.jupiter:junit-jupiter-engine:5.7.2")
    contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')

    contractTestCompile group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents
    contractTestCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: versions.httpComponents
    contractTestCompile group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestCompile group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
    contractTestRuntime group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestRuntime group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

    contractTestCompile sourceSets.main.runtimeClasspath
    contractTestCompile sourceSets.test.runtimeClasspath
}

test {
    exclude '**/HealthCheckFunctionalTest.class'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    include "uk/gov/hmcts/**/smoketest/**"
}


task functional(type: Test, description: 'Runs the functional tests', group: 'Verification') {
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    finalizedBy aggregate
}
task contract(type: Test) {
    group = 'Delivery pipeline'
    description = 'Pact consumer tests for Fee Register'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include "uk/gov/hmcts/reform/divorce/feeandpaymentservice/**"
    systemProperty 'pact.rootDir', "pacts"
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")

}
//below task for pipeline
task runAndPublishConsumerPactTests(type: Test){
    logger.lifecycle("Runs consumer contract tests for Fee register client")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

project.ext {
    pacticipant = 'divorce_feeAndPaymentService_feeRegister'
    pacticipantVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
    broker {
        pactBrokerUrl = System.getProperty("pact.broker.url") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'build/pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
        version = project.pacticipantVersion
    }
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

def sonarExclusions = [
        '**uk/gov/hmcts/reform/divorce/feepayment/model/*',
        '**uk/gov/hmcts/reform/divorce/feepayment/FeesPaymentServiceApplication.java'
]

sonarqube {
    properties {
        property "sonar.projectKey", "DivorceFeesAndPaymentsService"
        property "sonar.projectName", "Divorce :: Fees and Payments Service"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

def debug = System.getProperty("debug")
run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5010']
    }
}

task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

